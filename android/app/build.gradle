apply plugin: 'com.android.application'

def getOsmocomVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
        workingDir = file('../../osmocom-bb')
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "edu.uw.cs.seaglass.app"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "0.7.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi-v7a'
        }
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "OsmocomVersion", "\"${getOsmocomVersion()}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs 'prebuilt'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

repositories {
    maven { url 'https://jitpack.io' }
}

dependencies {
    def room_version = '2.0.0-beta01'
    def nav_version = "1.0.0-alpha09"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.0-beta01'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "android.arch.navigation:navigation-fragment:$nav_version"
    implementation "android.arch.navigation:navigation-ui:$nav_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0-beta01'
    annotationProcessor "androidx.room:room-compiler:$room_version"
    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'
}
